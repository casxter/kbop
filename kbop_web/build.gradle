group 'com.kbop'
version '1.0'

def jarFileName = 'kbop_web.jar'


buildscript {
    repositories {
        // jcenter()
        maven { url "http://maven.aliyun.com/nexus/content/groups/public/" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.7.RELEASE")
        classpath 'org.hidetake:gradle-ssh-plugin:2.9.0'
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'org.hidetake.ssh'

sourceCompatibility = 1.8
targetCompatibility = 1.8

//跳过测试
test.enabled = false

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}
//统一使用log4j2
configurations.all {
    exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
    exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    exclude group: 'org.springframework.boot', module: 'logback-classic'
}
//可执行jar
springBoot {
    executable = true
}

jar {
    baseName = 'kbop_web'
    archiveName = jarFileName
    version = '0.1.0'
}


repositories {
    maven { url "http://maven.aliyun.com/nexus/content/groups/public/" }
    mavenCentral()
    maven { url "http://repo.spring.io/release" }
}

dependencies {
    //apache commons
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'
    compile group: 'commons-io', name: 'commons-io', version: '2.5'
    compile group: 'com.sun.mail', name: 'javax.mail', version: '1.5.6'
    compile group: 'javax.activation', name: 'activation', version: '1.1.1'
    compile group: 'com.google.guava', name: 'guava', version: '23.0'
    compile group: 'com.alibaba', name: 'fastjson', version: '1.2.38'

    //spring boot
    compile("org.springframework.boot:spring-boot-starter")
    compile("org.springframework.boot:spring-boot-starter-aop")
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-jetty")
    compile("org.springframework.boot:spring-boot-starter-actuator")
    compile("org.springframework.boot:spring-boot-starter-log4j2")

    //data
    compile("mysql:mysql-connector-java")

    compile 'com.alibaba:druid-spring-boot-starter:1.1.3'
    compile("org.mybatis.spring.boot:mybatis-spring-boot-starter:1.1.1")

    compile("com.github.pagehelper:pagehelper-spring-boot-starter:1.2.1")
//    compile group: 'redis.clients', name: 'jedis', version: '2.9.0'

    //swagger
    compile group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.7.0'
    compile group: 'io.springfox', name: 'springfox-swagger2', version: '2.7.0'

    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile group: 'junit', name: 'junit', version: "${junitVersion}"
}

remotes {
    myvps {
        host = '23.106.135.43'
        user = 'kbop'
        port = 26973
        password = '980c9b2d5336f2bf'
    }
    localnetwork {
        host = '192.168.1.197'
        user = 'kbop'
        password = '980c9b2d5336f2bf'
    }
}

task uname {
    doLast {
        ssh.run {
            session(remotes.myvps) {
                execute 'uname -a'
            }
        }
    }
}

task uploadJar {
    doLast {
        ssh.run {
            session(remotes.myvps) {
                put from: "${buildDir}/libs/${jarFileName}", into: '/home/kbop'
            }
        }
    }
}

task uploadJarlocalnetwork {
    doLast {
        ssh.run {
            session(remotes.localnetwork) {
                put from: "${buildDir}/libs/${jarFileName}", into: '/home/work'
            }
        }
    }
}

task reloadKbop {
    doLast {
        ssh.run {
            session(remotes.myvps) {
                execute 'echo 980c9b2d5336f2bf | sudo -S systemctl restart kbop'
                execute 'echo 980c9b2d5336f2bf | sudo -S systemctl status kbop'
            }
        }
    }
}

task deploy {
    dependsOn assemble
    dependsOn uploadJar
    dependsOn reloadKbop
}



